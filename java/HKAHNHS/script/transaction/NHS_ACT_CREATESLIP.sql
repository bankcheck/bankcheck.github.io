CREATE OR REPLACE FUNCTION "NHS_ACT_CREATESLIP" (
	i_ACTION   IN VARCHAR2,
	i_SLPTYPE  IN VARCHAR2,
	i_DOCCODE  IN VARCHAR2,
	i_REGOPCAT IN VARCHAR2,
	i_DPTCODE  IN VARCHAR2,
	i_PATFNAME IN VARCHAR2,
	i_PATGNAME IN VARCHAR2,
	i_PATNO    IN VARCHAR2,
	i_REGID    IN VARCHAR2,
	i_ALERT    IN VARCHAR2,
	i_STECODE  IN VARCHAR2,
	i_USRID    IN VARCHAR2,
	O_ERRMSG   OUT VARCHAR2
)
	RETURN NUMBER
AS
	O_ERRCODE NUMBER := -1;
	v_COUNT NUMBER;
	v_SLIPNO VARCHAR2(20);
	v_SLPTYPE VARCHAR2(1);
	v_SLPDATE DATE;
	v_SLPDATE_STR VARCHAR2(10);
	v_InitialAssessed VARCHAR2(10);
	v_REMARK VARCHAR2(200);
	v_QUOTAD NUMBER := 0;
	v_QUOTAM NUMBER := 0;
	v_QUOTAY NUMBER := 0;
	v_CheckDateTime NUMBER;

	SLIP_STATUS_OPEN VARCHAR2(1) := 'A';
	SLIP_TYPE_OUTPATIENT VARCHAR2(1) := 'O';
BEGIN
	IF i_ACTION = 'ADD' THEN
		v_SLPTYPE := CASE WHEN i_PATNO IS NULL THEN SLIP_TYPE_OUTPATIENT ELSE NVL(i_SLPTYPE, SLIP_TYPE_OUTPATIENT) END;

		IF i_PATNO IS NOT NULL THEN
			SELECT COUNT(1) INTO v_COUNT FROM SLIP S, SLIP_EXTRA E
			WHERE S.SLPNO = E.SLPNO AND S.PATNO = i_PATNO AND S.SLPTYPE = v_SLPTYPE AND S.DOCCODE = i_DOCCODE AND E.SLPDATE IS NOT NULL AND E.SLPDATE > SYSDATE - (1 / 86400);

			IF v_COUNT > 0 THEN
				O_ERRCODE := -1;
				O_ERRMSG := 'Fail to create new slip in short period of time. Please try again later!';
				RETURN O_ERRCODE;
			END IF;
		END IF;

		IF i_ALERT IS NOT NULL AND GET_CURRENT_STECODE = 'HKAH' THEN
			v_InitialAssessed := i_ALERT;
			v_SLPDATE := SYSDATE;
			IF v_InitialAssessed = '20' THEN
				v_CheckDateTime := TO_NUMBER(TO_CHAR(v_SLPDATE, 'HH24MI'));

				IF v_CheckDateTime >= 1800 THEN
					v_InitialAssessed := '23';
				ELSIF v_CheckDateTime >= 1120 THEN
					v_InitialAssessed := '22';
				ELSE
					v_InitialAssessed := '21';
				END IF;
			END IF;

			IF v_InitialAssessed IN ('10', '15', '20', '21', '22', '23') THEN
				v_REMARK := 'C-19 TEST';
			ELSIF v_InitialAssessed IN ('30', '31', '32', '33') THEN
				v_REMARK := 'FLU VACC';
			ELSIF v_InitialAssessed IN ('40', '41', '42', '43') THEN
				v_REMARK := 'FLU MIST';
			ELSIF v_InitialAssessed IN ('100') THEN
				v_REMARK := 'SHINGRIX VACC';
			END IF;

			SELECT COUNT(1) INTO v_COUNT FROM BOOKINGALERT WHERE BKAID = i_ALERT AND (BKAQUOTAD = -1 OR BKAQUOTAM = -1 OR BKAQUOTAY = -1);
			IF v_COUNT > 0 THEN
				v_COUNT := NHS_UTL_ALERTQUOTA(v_InitialAssessed, i_DOCCODE, v_SLPDATE);
				IF v_COUNT = 0 THEN
					O_ERRCODE := -1;
					O_ERRMSG := 'Quota is full.';
					RETURN O_ERRCODE;
				END IF;
			END IF;
		END IF;

		v_SLIPNO := GENERATE_SLIP_NO;

		INSERT INTO SLIP(
			SLPNO,
			SLPTYPE,
			PATNO,
			SLPFNAME,
			SLPGNAME,
			REGID,
			DOCCODE,
			DPTCODE,
			SLPSTS,
			SLPSEQ,
			SLPPSEQ,
			SLPHDISC,
			SLPDDISC,
			SLPSDISC,
			SLPPAMT,
			SLPCAMT,
			SLPDAMT,
			SLPREMARK,
			REGOPCAT,
			STECODE,
			USRID
		) VALUES (
			v_SLIPNO,
			v_SLPTYPE,
			i_PATNO,
			i_PATFNAME,
			i_PATGNAME,
			i_REGID,
			i_DOCCODE,
			i_DPTCODE,
			SLIP_STATUS_OPEN,
			1,
			1,
			0,
			0,
			0,
			0,
			0,
			0,
			v_REMARK,
			i_REGOPCAT,
			i_STECODE,
			i_USRID
		);

		INSERT INTO SLIP_EXTRA(
			SLPNO,
			SLPDATE
		) VALUES (
			v_SLIPNO,
			SYSDATE
		);

		-- Automate Slip Category
		v_COUNT := NHS_UTL_AUTOSLIPCAT(v_SLIPNO, NULL);

		IF i_ALERT IS NOT NULL AND GET_CURRENT_STECODE = 'HKAH' THEN
			SELECT COUNT(1) INTO v_COUNT FROM BOOKINGALERT WHERE BKAID = i_ALERT AND (BKAQUOTAD = -1 OR BKAQUOTAM = -1 OR BKAQUOTAY = -1);
			IF v_COUNT = 1 THEN
				SELECT BKAQUOTAD, BKAQUOTAM, BKAQUOTAY INTO v_QUOTAD, v_QUOTAM, v_QUOTAY FROM BOOKINGALERT WHERE BKAID = i_ALERT AND (BKAQUOTAD = -1 OR BKAQUOTAM = -1 OR BKAQUOTAY = -1);
			END IF;

			IF (i_ALERT = '31' OR i_ALERT = '41') OR v_QUOTAY = -1 THEN
				v_SLPDATE_STR := TO_CHAR(v_SLPDATE, 'YYYY');
			ELSIF v_QUOTAM = -1 THEN
				v_SLPDATE_STR := TO_CHAR(v_SLPDATE, 'MM/YYYY');
			ELSE
				v_SLPDATE_STR := TO_CHAR(v_SLPDATE, 'DD/MM/YYYY');
			END IF;

			IF (i_ALERT = '31' OR i_ALERT = '41') AND i_DOCCODE IS NOT NULL THEN
				SELECT COUNT(1) INTO v_COUNT FROM HPSTATUS WHERE HPTYPE = 'QUOTAYEAR' AND HPKEY = i_ALERT AND HPSTATUS = v_SLPDATE_STR || '_' || i_DOCCODE;
				IF v_COUNT = 1 THEN
					UPDATE HPSTATUS SET HPRMK1 = NVL(HPRMK1, 0) + 1 WHERE HPTYPE = 'QUOTAYEAR' AND HPKEY = i_ALERT AND HPSTATUS = v_SLPDATE_STR || '_' || i_DOCCODE;
				END IF;

				O_ERRCODE := NHS_ACT_SYSLOG(i_ALERT, 'QuotaCheck', v_SLPDATE_STR || '_' || i_DOCCODE, v_SLIPNO, i_USRID, NULL, O_ERRMSG);
			ELSE
				SELECT COUNT(1) INTO v_COUNT FROM HPSTATUS WHERE HPTYPE IN ('QUOTAYEAR', 'QUOTAMONTH', 'QUOTADAILY') AND HPKEY = i_ALERT AND HPSTATUS = v_SLPDATE_STR;
				IF v_COUNT = 1 THEN
					UPDATE HPSTATUS SET HPRMK1 = NVL(HPRMK1, 0) + 1 WHERE HPTYPE IN ('QUOTAYEAR', 'QUOTAMONTH', 'QUOTADAILY') AND HPKEY = i_ALERT AND HPSTATUS = v_SLPDATE_STR;
				END IF;

				O_ERRCODE := NHS_ACT_SYSLOG(i_ALERT, 'QuotaCheck', v_SLPDATE_STR, v_SLIPNO, i_USRID, NULL, O_ERRMSG);
			END IF;
		END IF;
	END IF;

	IF SQL%ROWCOUNT = 1 THEN
		O_ERRCODE := v_SLIPNO;
		O_ERRMSG  := 'OK';
	END IF;

	RETURN O_ERRCODE;
EXCEPTION
WHEN OTHERS THEN
	ROLLBACK;
	DBMS_OUTPUT.PUT_LINE('An ERROR was encountered - '||SQLCODE||' -ERROR- '||SQLERRM);
	O_ERRMSG := 'An ERROR was encountered - '||SQLCODE||' -ERROR- '||SQLERRM;
	RETURN -1;
END NHS_ACT_CREATESLIP;
/
